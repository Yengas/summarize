group 'edu.yengas'
version '1.0-SNAPSHOT'

apply plugin: 'java'

sourceCompatibility = 1.8

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.0'
        classpath 'se.transmode.gradle:gradle-docker:1.2'
    }
}

dependencies {
    compile group: 'com.typesafe.akka', name: 'akka-stream_2.12', version: '2.5.6'
    compile group: 'zemberek-nlp', name: 'lang-id', version: '0.11.1'
    compile group: 'zemberek-nlp', name: 'tokenization', version: '0.11.1'
    compile group: 'zemberek-nlp', name: 'morphology', version: '0.11.1'
    testCompile group: 'com.typesafe.akka', name: 'akka-stream-testkit_2.12', version:'2.5.6'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

// Add the MainClass attribute to the jar file.
jar{
    manifest{
        attributes(
            'Main-Class': 'edu.yengas.ozet.Main',
        )
    }
}

repositories {
    mavenCentral()
    maven{
        url "https://raw.github.com/ahmetaa/maven-repo/master"
        name "ahmetaa Maven Repo on Github"
    }
}

apply plugin: 'com.github.johnrengelman.shadow'

shadowJar {
    transform(com.github.jengelman.gradle.plugins.shadow.transformers.AppendingTransformer) {
        resource = 'reference.conf'
    }
}

apply plugin: 'docker'

task buildDocker(type: Docker) {
    baseImage = 'java:openjdk-8-jre-alpine'
    push = project.hasProperty('push')
    tag = 'yengas/summarize'

    addFile {
        from shadowJar
        rename {'summarize.jar'}
    }

    entryPoint(['java', '-jar', '/summarize.jar'])
}

buildDocker.dependsOn(build)
